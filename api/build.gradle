group 'com.softwareverde'
version '1.0.0'

apply plugin: 'java'

sourceCompatibility = 1.7

task makeJar(type: Jar) {
    manifest {
        attributes  'Implementation-Title': 'Http Servlet Example',
                    'Implementation-Version': version,
                    'Main-Class': 'com.softwareverde.example.Main'
    }
    baseName = project.name + '-server'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile group: 'com.github.softwareverde', name: 'java-db-mysql', version: 'v1.+'
    compile group: 'com.github.softwareverde', name: 'http-servlet', version: 'v1.+'
    compile group: 'com.github.softwareverde', name: 'java-socket', version: 'v0.+'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events "passed", "skipped", "failed", "standardOut"
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat "full"
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

